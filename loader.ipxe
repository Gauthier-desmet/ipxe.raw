#!ipxe

echo -- loader.ipxe --

set netboot     https://plmlab.math.cnrs.fr/resinfo/ANF/2019/ADA/ipxe
set config_base https://resinfo.pages.math.cnrs.fr/ANF/2019/ADA/ipxe
set https_base  https://delavennat.perso.math.cnrs.fr
set script_url  ${netboot}/ipxe.raw/raw/master/loader.ipxe
set host_info   manufacturer=${manufacturer}&product=${product}&uuid=${uuid}&mac=${mac}&serial=${serial}
set host_loader ${config_base}/${mac:hexhyp}/loader.ipxe?base64=${host_info:base64}

#
# if no loader is defined on the host, use a menu
#
chain ${host_loader} ||

menu Openstack iPXE Loader 
item refresh_menu  Refresh Menu
item --gap
item --gap +------------------------------------------------------------------------+
item --gap |.................................... Boot ..............................|
item --gap +------------------------------------------------------------------------+
item --gap
item --key 1 vda     [1] VDA (iPXE volume)
item --key 2 vdb     [2] VDB (System volume)
item --gap
item --gap +------------------------------------------------------------------------+
item --gap |................................... Install ............................|
item --gap +------------------------------------------------------------------------+
item --key 3 rhcos   [3] Red Hat Enterprise Linux CoreOS (RHCOS) - 4.1.0
item --key 4 rancher [4] RancherOS - 1.5.2
item --key 5 alpine  [5] AlpineLinux - 3.9
item --gap
item --key 6 shell   [6] SHELL (Shell iPXE)
item --gap
#
# cf http://ipxe.org/cmd/choose
# --timeout (miliseconds)
#
choose --default vdb --timeout 10000 choice && goto ${choice}

:vda
sanboot --no-describe --drive 0x80 || goto refresh_menu

:vdb
sanboot --no-describe --drive 0x81 || goto refresh_menu

###########################################################################################
#
# Red Hat Enterprise Linux CoreOS
#
###########################################################################################
:rhcos
#
# - https://github.com/openshift/installer/blob/master/docs/user/openstack/README.md
# - https://docs.openshift.com/container-platform/4.1/installing/installing_bare_metal/installing-bare-metal.html#minimum-resource-requirements_installing-bare-metal
# - https://mirror.openshift.com/pub/openshift-v4/dependencies/rhcos/4.1/4.1.0/
#
set rhcos_version      4.1.0
set rhcos_path         openshift-v4/dependencies/rhcos/4.1/${rhcos_version}
set rhcos_mirror       ${https_base}/mirror/${rhcos_path}
set rhcos_kernel       ${rhcos_mirror}/rhcos-${rhcos_version}-x86_64-installer-kernel
set rhcos_initrd       ${rhcos_mirror}/rhcos-${rhcos_version}-x86_64-installer-initramfs.img
set rhcos_image_url    ${rhcos_mirror}/rhcos-${rhcos_version}-x86_64-metal-bios.raw.gz
set rhcos_ignition_url ${https_base}/ignition/bootstrap.ign
set rhcos_install_dev  vdb
set rhcos_append       ip=dhcp \
                       rd.neednet=1 \
                       initrd="${rhcos_initrd}" \
                       console=tty0 \
                       console=ttyS0 \
                       coreos.inst=yes \
                       coreos.inst.install_dev=${rhcos_install_dev} \
                       coreos.inst.image_url=${rhcos_image_url} \
                       coreos.inst.ignition_url=${rhcos_ignition_url}

#show rhcos_initrd
#show rhcos_kernel
#show rhcos_append

imgfetch --name rhcos_initrd ${rhcos_initrd} || goto shell
imgfetch --name rhcos_kernel ${rhcos_kernel} || goto shell
imgexec rhcos_kernel ${rhcos_append}         || goto shell

###########################################################################################
#
# RancherOS
#
###########################################################################################
:rancher
set rancher_version 1.5.2
set rancher_path    rancher/os/v${rancher_version}
set rancher_mirror  ${https_base}/mirror/${rancher_path}
set rancher_kernel  ${rancher_mirror}/vmlinuz
set rancher_initrd  ${rancher_mirror}/initrd
set rancher_rootfs  ${rancher_mirror}/rootfs.tar.gz
set rancher_config  ${config_base}/${mac:hexhyp}/rancher/os/cloud-config.yaml

set rancher_append  rancher.network.interfaces.eth*.dhcp=true \
                    rancher.cloud_init.datasources=[url:${rancher_config}] \
                    rancher.autologin=tty1 \
#                    rancher.debug=true \
#                    console=tty0 \
#                    console=ttyS0,115200n8
#                    rancher.state.mdadm_scan \
#                    rancher.state.wait \
#                    rancher.state.dev=LABEL=RANCHER_STATE \
#                    rancher.state.autoformat=[/dev/vdb] \

show rancher_append
prompt Press any key to continue

imgfetch --name rancher_initrd ${rancher_initrd} || goto shell
imgfetch --name rancher_kernel ${rancher_kernel} || goto shell
imgexec rancher_kernel ${rancher_append}         || goto shell

goto refresh_menu

###########################################################################################
#
# Alpine Linux
#
###########################################################################################
:alpine
goto refresh_menu

:shell:
shell

:refresh_menu
chain ${script_url}